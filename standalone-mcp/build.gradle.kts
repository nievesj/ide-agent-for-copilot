import org.jetbrains.intellij.platform.gradle.tasks.VerifyPluginTask.FailureLevel

plugins {
    id("java")
    id("org.jetbrains.intellij.platform") version "2.13.1"
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        val localPath = providers.gradleProperty("intellijPlatform.localPath").orNull
        if (localPath != null) {
            local(localPath)
        } else {
            intellijIdeaUltimate("2025.3")
        }
        testFramework(org.jetbrains.intellij.platform.gradle.TestFrameworkType.Platform)
        bundledPlugin("com.intellij.java")
        bundledPlugin("Git4Idea")
        bundledPlugin("org.jetbrains.plugins.terminal")
    }

    // Compile against plugin-core to reuse PsiBridgeService, ToolRegistry, etc.
    compileOnly(project(":plugin-core"))

    // JSON processing (Gson)
    implementation("com.google.code.gson:gson:${providers.gradleProperty("gsonVersion").get()}")
    // Force annotations version to match the platform
    implementation("org.jetbrains:annotations:${providers.gradleProperty("annotationsVersion").get()}")

    testImplementation("org.junit.jupiter:junit-jupiter:${providers.gradleProperty("junitVersion").get()}")
    testImplementation("junit:junit:${providers.gradleProperty("junit4Version").get()}")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${providers.gradleProperty("junitVersion").get()}")
}

configurations.all {
    resolutionStrategy.force("org.jetbrains:annotations:${providers.gradleProperty("annotationsVersion").get()}")
}

// Repackage plugin-core without its plugin.xml descriptor.
// standalone-mcp has its own plugin.xml; including plugin-core's would cause
// "multiple plugin descriptors" errors during verifyPlugin.
val repackagePluginCore by tasks.registering(Jar::class) {
    archiveBaseName.set("plugin-core-classes")
    dependsOn(project(":plugin-core").tasks.named("jar"))
    from(provider {
        zipTree(project(":plugin-core").tasks.named("jar").get().outputs.files.singleFile)
    }) {
        // Exclude plugin-core's plugin descriptor and optional-dependency feature XMLs.
        // standalone-mcp declares its own plugin.xml and feature XMLs so they resolve
        // correctly from the primary JAR when the plugin verifier scans config-file attributes.
        exclude("META-INF/plugin.xml")
        exclude("META-INF/git-features.xml")
        exclude("META-INF/java-features.xml")
        exclude("META-INF/gradle-features.xml")
        exclude("META-INF/maven-features.xml")
        exclude("META-INF/sonarlint-features.xml")
        exclude("META-INF/terminal-features.xml")
    }
}

// plugin-core classes (without plugin.xml) must be on the runtime classpath so that
// buildSearchableOptions can instantiate service/configurable types defined in plugin-core.
// IPP's buildSearchableOptions uses a fresh sandbox separate from the regular prepareSandbox
// output, so it does not pick up the jar copied via doLast — it needs the jar on the runtime
// configuration so IPP bundles it automatically.
dependencies {
    runtimeOnly(files(repackagePluginCore))
}

// Include plugin-core classes in the standalone plugin
tasks.named("prepareSandbox") {
    dependsOn(repackagePluginCore)
    doLast {
        val coreJar = repackagePluginCore.get().outputs.files.singleFile
        val ideDirs = File(
            layout.buildDirectory.asFile.get(),
            "idea-sandbox"
        ).listFiles { f -> f.isDirectory && f.name.startsWith("IU-") }
        ideDirs?.forEach { ideDir ->
            val sandboxLib = File(ideDir, "plugins/standalone-mcp/lib")
            sandboxLib.mkdirs()
            coreJar.copyTo(File(sandboxLib, "plugin-core.jar"), overwrite = true)
        }
    }
}

tasks.named<Zip>("buildPlugin") {
    archiveBaseName.set("IDE-MCP-Server")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(repackagePluginCore)
    from(repackagePluginCore) {
        into("lib")
        rename { "plugin-core.jar" }
    }
    // Also include mcp-server.jar for stdio-based agents
    dependsOn(project(":mcp-server").tasks.named("jar"))
    from(project(":mcp-server").tasks.named("jar")) {
        into("lib")
        rename { "mcp-server.jar" }
    }
}

intellijPlatform {
    pluginConfiguration {
        id = "com.github.catatafishen.idemcpserver"
        name = "IDE MCP Server"
        version = project.version.toString()

        ideaVersion {
            sinceBuild = "253"
            untilBuild = provider { null }
        }
    }

    pluginVerification {
        failureLevel.set(
            listOf(
                FailureLevel.INVALID_PLUGIN,
                FailureLevel.INTERNAL_API_USAGES,
                FailureLevel.OVERRIDE_ONLY_API_USAGES,
                FailureLevel.NON_EXTENDABLE_API_USAGES,
                FailureLevel.PLUGIN_STRUCTURE_WARNINGS,
            )
        )
        ides {
            recommended()
        }
    }
}

tasks {
    withType<Jar> {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    withType<JavaCompile> {
        options.compilerArgs.add("-Xlint:deprecation")
    }

    test {
        useJUnitPlatform()
    }
}
